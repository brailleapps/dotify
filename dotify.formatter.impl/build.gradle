apply plugin: "java"
apply plugin: "maven"
//custom plugins
apply plugin: "bundle"
apply plugin: "signing"

group = "org.daisy.dotify"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

sourceSets {
    main {
        java { srcDir "src" }
        resources { srcDir "src" }
    }
    test {
        java { srcDir "test" }
        resources { srcDir "test" }
    }
}

configurations {
    provided {
        transitive = true
        visible = true
    }
}

sourceSets {
    main.compileClasspath += configurations.provided
    main.runtimeClasspath -= configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

dependencies {
    compile group: "org.daisy.dotify", name: "dotify.api", version: "1.0.0"
    compile "org.daisy.dotify:dotify.common:1.0.0"
    compile group: "biz.aQute.bnd", name: "annotation", version: "2.3.0"
    runtime group: 'woodstox', name: 'wstx-lgpl', version: '3.2.7'
    testCompile group: "junit", name: "junit", version: "4.7"
    testCompile group: "org.daisy.braille", name: "brailleUtils-core", version: "2.0-SNAPSHOT"
    
	testRuntime group: group, name:'dotify.hyphenator.impl', version:'1.0-SNAPSHOT'
	testRuntime group: group, name:'dotify.text.impl', version:'1.+'
	testRuntime group: group, name:'dotify.translator.impl', version:'1.0.0-SNAPSHOT'
}

compileJava {
    options.encoding = "UTF-8"
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    // FIXME: "bootstrap class path not set in conjunction with -source 1.6"
    // options.bootClasspath = "/usr/lib/jvm/java-6-openjdk/jre/lib/rt.jar"
}

compileTestJava {
	options.encoding = 'UTF-8'
}

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.5" }
}

bundle {
    instructions << [
        "-include": file('bnd.bnd'),
    ]
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name 'dotify.formatter.impl'
                packaging 'jar'
                description 'Dotify Braille Formatter'
                url 'https://github.com/joeha480/dotify/'
                scm {
                    connection 'scm:git:https://github.com/joeha480/dotify.git'
                    developerConnection 'scm:git:https://github.com/joeha480/dotify.git'
                    url 'https://github.com/joeha480/dotify'
                }
                licenses {
                    license {
                        name 'LGPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                    }
                }
                developers {
                    developer {
                        id 'joel'
                        name 'Joel HÃ¥kansson'
                    }
                }
            }
        }
    }
}