apply plugin: 'application'
apply plugin: 'maven'

mainClassName = "org.daisy.dotify.cli.DotifyCLI"
startScripts.applicationName = 'dotify'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

group = "org.daisy.dotify"
version = "3.0-SNAPSHOT"

ext {
	versions = [
			'osgi'               : '5.0.0',
			'junit'              : '4.7',
			'felix'              : '4.4.1',
			'paxExam'            : '3.5.0', //4.4.0 requires Java 7
			'paxUrl'             : '2.3.0',
	]
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'src'
		}
	}
	test {
		java {
			srcDir 'test'
			srcDir 'integrationtest'
		}
		resources {
			srcDir 'test'
			srcDir 'integrationtest'
		}
	}
}

jar.doFirst {
	manifest {
    	attributes ('Built-By': System.getProperty("user.name"),
    	'Main-Class': mainClassName,
    	'Built-On': new Date().format("yyyy-MM-dd"),
		'Repository-Revision': "$repositoryRevision",
		'Implementation-Title': "$releaseName",
		'Implementation-Version': "$releaseVersion",
		'Implementation-URL': "$releaseURL",
		'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')			//'${core.classpath.attribute}'
		)
	}
	from sourceSets.main.allJava
	baseName = 'dotify-cli'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile 'org.daisy.braille:brailleUtils-ui:2.0.0-SNAPSHOT'
	compile 'org.daisy.braille:braille-utils.pef-tools:1.0.1'
	compile 'org.daisy.dotify:dotify.api:1.5.0'
	compile 'org.daisy.dotify:dotify.common:1.2.0'
	compile 'org.daisy.dotify:dotify.task-api:1.0.0'
	

	runtime 'org.daisy.dotify:dotify.formatter.impl:1.3.0'
	runtime 'org.daisy.dotify:dotify.hyphenator.impl:1.0.0'
	runtime 'org.daisy.dotify:dotify.text.impl:1.0.0'
	runtime 'org.daisy.dotify:dotify.translator.impl:1.3.0'
	runtime 'org.daisy.dotify:dotify.task.impl:1.0.1'
	runtime group: "com.googlecode.texhyphj", name: "texhyphj", version: "1.2"
	runtime 'xerces:xercesImpl:2.11.0'

	testCompile group: 'junit', name: 'junit', version: "$versions.junit"

	testCompile "javax.inject:javax.inject:1"
	testCompile "org.ops4j.pax.exam:pax-exam-junit4:$versions.paxExam"
	testRuntime "org.ops4j.pax.exam:pax-exam-container-native:$versions.paxExam"
	testRuntime "org.ops4j.pax.exam:pax-exam-link-mvn:$versions.paxExam"
	testRuntime "org.ops4j.pax.url:pax-url-aether:$versions.paxUrl"
	testRuntime "org.ops4j.pax.url:pax-url-reference:$versions.paxUrl"
	testRuntime "org.ops4j.pax.url:pax-url-wrap:$versions.paxUrl"
	testRuntime "org.apache.felix:org.apache.felix.framework:$versions.felix"
	testCompile("org.osgi:org.osgi.core:$versions.osgi")
	testCompile("org.osgi:org.osgi.compendium:$versions.osgi")
}
task xsltdoc() << {
	ant {
		xslt (
			basedir: "src",
			destdir: (new File(docsDir, "xsltdoc")),
			style: "build-xslt-doc.xsl",
			includes: "**/*.xsl",
			excludes:"lib, ant-build, build-xslt-doc.xsl, build",
			filenameparameter: "filename"
		) {
			mapper (
				type: "package",
				from: "*.xsl",
				to: "*.html"
			)
			xmlcatalog {
				dtd (
					publicId: "-//W3C//DTD XHTML 1.0 Strict//EN",
					location: "ant-build/resources/dtd/xhtml10/xhtml1-strict.dtd"
				)
			}
		}
	}
}

javadoc.dependsOn(xsltdoc)